buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		classpath "org.hibernate:hibernate-gradle-plugin:5.6.9.Final"
	}
}


plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'org.springframework.experimental.aot' version '0.12.0'
}

apply plugin: 'org.hibernate.orm'

hibernate {
	enhance {
		enableLazyInitialization= true
		enableDirtyTracking = true
		enableAssociationManagement = true
		enableExtendedEnhancement = false
	}
}

group = 'com.gamers'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	maven { url 'https://repo.spring.io/release' }
	mavenCentral()
}

bootJar {
	layered {
		enabled = true
	}
}

bootBuildImage {
	builder = "paketobuildpacks/builder:tiny"
	environment = [
			"BP_NATIVE_IMAGE" : "true"
	]
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-core'
		exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-websocket'
	}
	implementation "org.apache.tomcat.experimental:tomcat-embed-programmatic:${dependencyManagement.importedProperties["tomcat.version"]}"
	implementation 'org.springframework.session:spring-session-jdbc'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.6.9'
	implementation 'org.springdoc:springdoc-openapi-native:1.6.9'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-hibernate5'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}
